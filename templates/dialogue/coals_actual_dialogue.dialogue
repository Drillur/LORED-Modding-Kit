~ coal
do set_pose("coal_hub")
COAL: [[Don't mind me! Just diggin' around!|Sure do love me some diggin!|Dig, dig!]]
- How does Input work?
	if is_section_read("input_first")
		=>< input_first
	else
		=>< input_second
- Where is your safety wheel? [if is_safety_wheel_unlocked()]
	=> coal_wheres_your_safety_wheel
=> coal


#region Input tutorial


~ input_first
COAL: Input is like, uhhmm... the amount of ingredients required to start working!
COAL: Well--[i]actually![/i] Input is a LORED attribute! It [i]multiplies[/i] the amount of resources required to work a specific job!
COAL: You might not have the Upgrade to view our list of jobs, yet... but if you don't, you will soon!
COAL: Most jobs require 1 ingredient of something. And then when we level up, our Input doubles, so it would be 2, or 2 ^ (our level - 1).
COAL: See even though I as a really cool and attractive LORED have an Input attribute, my main job (which is to make {{get_currency_name("coal")}}) doesn't have any base cost!!
COAL: I'm one of the only LOREDs who doesn't use the Input attribute at all!!!
COAL: Sorry, I was rambling. Have any questions?
=> input_questions


~ input_second
COAL: Uh oh! Still need help with Input, huh?
=> input_questions


~ input_questions
COAL: Ask me anything!
- Never mind.
	COAL: Aight!
- How much Input do you have?
	{{get_input("coal")}}. You can see for yourself in the {{get_icon_text("side_bar")}} LORED Widget panel! ... That is, if you've unlocked it.
	- Oh okay, thanks.
	- Why is there a dash between those two numbers?
		COAL: Oh because it's a range. Especially when you just start the game, the gap between the two numbers is... 0. So it might look like "1 - 1" which is weird.
		COAL: It's because later our Input might be 1,000 - 2,000. So at the start of every job, we roll for a value within that range.
		- Oh, okay.
		- I don't get it.
			COAL: No worries. It just means that there is a low and a high value for Input.
			COAL: Overall--you want to get both of these numbers lower. Keep that in mind!
- Why don't you use your Input attribute?
	COAL: Because my main job has no ingredient cost!
	COAL: Almost every other LORED makes use of that attribute.
- Are there any jobs which have an ingredient cost of something other than 1?
	COAL: Yup... That's how that sick, twisted Dev plays with your emotions.
	if is_lored_unlocked("oil")
		COAL: {{get_lored_name("oil")}}, that disgusting baby--he starts off with 1 Input like the rest of us, but [i]his job[/i] has a very low ingredient cost.
	else
		COAL: You cool with spoilers?
		- Yes.
			COAL: You're gonna meet a disgusting baby later on named {{get_lored_name("oil")}}. He starts off with 1 Input like the rest of us, but [i]his job[/i] has a very low ingredient cost.
		- No.
			COAL: Well, then, ask me that again after you've played some more!
			COAL: And maybe you'll just find it out on your own. :)
- Can you explain Input again? => input_first
- What should I do about Input?
	COAL: Get it as low as possible!!! It means you won't be [i]spending[/i] as much stuff!
- Quiz me!
	COAL: Oh, uhm... Okay!
	COAL: If a LORED's job required 1 of something, but he had 8 Input, how much of that something would be need?
	- 1. =>< failed_quiz
	- 4. =>< failed_quiz
	- 8.
		COAL: Sick!!! You nailed it, chief!
	- 64. =>< failed_quiz
- I have other questions!
	COAL: If you message the Dev on Discord, he'll add your question to this list.
	DEV: I'm fr bruh. MSG ME, I DARE U
COAL: Got anymore questions about Input?
- Yes. => input_questions
- No.
	COAL: That's what I'm talkin about!!! Good job, graduate!
	=> END!

~ failed_quiz
COAL: Nope! Input multiplies the base ingredient cost!
=> END

#endregion


#region Dating


~ coal_wheres_your_safety_wheel
COAL: My whuat?
- Your safety wheel.
COAL: Don't know what you're talking about, chief!
- Okay, then. => END
- All of the others have one.
	COAL: They.. they what?! Really??
	COAL: Why wasn't I given one?!
	- I don't know.
	COAL: Well, I didn't [i]want[/i] one before. Now that you've brought it up, though...
	COAL: Hey, {{get_lored_name("iron")}}! You know anything about this?
	IRON: About what?
	COAL: Why don't I have a--a, uh...
	COAL: What did you call it?
	- Safety wheel.
		COAL: Yeah. That. Why don't I have one of those?
		IRON: Oh! Well, you don't need it, do you, buddy?
		COAL: What?! Yes, I do!
		IRON: No, nonono. The safety wheel only shows the [b]Player[/b] there how many jobs we can work in a row.
		IRON: You don't make use of your [b]Input[/b] attribute at all! You don't [i]need[/i] a safety wheel, because you can work [b][i]indefinitely![/i][/b] Understand?
		COAL: Oh. That's right. I'm a freakin legend. I forgot.
		COAL: Yeah, he's right! I don't [i]need[/i] a stupid-ass safety wheel!
		COAL: Psh. Why'd you even ask somethin like that...? It's obvious.
		COAL: Anyway. Be seein ya around, pal!
		- Bye.
		- Cya.
		- Dueces.
		- Sayonara.
		=> END
	- Girlfriend.
		COAL: Yeah, that! Why don't I have a girlfriend? [wait=2][font_size=10]Was that what I was mad about?
		IRON: Uhm... I don't know if I can help you with that, buddy. Try asking... someone else.
		COAL: Drats. I need one of those [i]girlfriends[/i]! Now that you've pointed them out, I can't un-see the fact that I don't have one! They're so nice and.. and round!... gosh!
		=> coal_dating_help_hub


~ coal_dating_help_hub
if is_section_read("coal_dating_1") and is_section_read("coal_dating_2") and is_section_read("coal_dating_3")
	=> coal_dating_finale
else
	=> coal_dating_help_hub_2


~ coal_dating_finale
do DialogueHandbook.set_section_read_false("coal_dating_1")
do DialogueHandbook.set_section_read_false("coal_dating_2")
do DialogueHandbook.set_section_read_false("coal_dating_3")
COAL: Ugh... Now, what? I can't bear to ask another LORED. They don't know what they're talking about.
- Ask me.
	COAL: Y... you?
	- Me.
	COAL: Alright... Why don't... why [i]can't[/i] I get a girlfriend?
	- How often are you working on trying to get a girlfriend?
		COAL: Well, erm... A few hours every 6 months.
		- Are you sure it is a priority for you? Or is it just something you occasionally want?
		COAL: I guess... the second one you said.
		COAL: But, but! Everybody says that having a [i]wife[/i] and [i]family[/i] makes you [i]content![/i] I'm not content, so why don't I pursue that more? I'm just digging holes in the ground, it feels meaningless after awhile.
		- You need to decide what you want to do.
		COAL: Well, I want to make {{get_currency_name("coal")}}, but I also want to be [i]happy.[/i]
		COAL: Playing Paper Mario with my son, showing my daughter Avatar the Last Airbender--I would be the happiest man alive with kids.
		COAL: Hey, [b]Player[/b]? What if I get a wife and kids, and find it to be... boring?
		- Are you thinking of your future children as a source of entertainment, instead of human beings?
		COAL: I'm just afraid that I won't be able to commit to a family once I finally have one.
		COAL: My kids may all end up like {{get_lored_name("iron_ore")}}, or my wife may be... I don't know.. maybe she'll divorce me or something.
		- You shouldn't have kids, yet.
			COAL: Well, I know that.
			COAL: Can I change if I don't work on changing? Like, if I keep digging, will my brain work itself out so that I'll be a good father in the end?
			COAL: Look, I'm getting depressed, and this conversation is way past its bedtime.
			COAL: Thanks for talking with me, buddy. I'm glad you picked up this game for $5 on the Steam store and didn't pirate or otherwise [i]obtain[/i] it.
			- You're welcome?
		- Trust in yourself.
			COAL: You think I'll be able to commit when the time comes? When the captain calls, so to speak? Like, I'll be a sailor on the beach, and when the cap'n calls, I'll come aboard? I'll rise to the occasion? That whatcha mean?
			- Sure.
			- Close enough.
			- Ehh...
			COAL: Well, that's a positive thought! I hope you're right, dude... I really do.
			COAL: Thanks for helping me out with this shit. Sorry to have sprung it on you all of a sudden. ... Er... [font_size=10]why did I bring this up, anyway?
			COAL: See you around, chief.
			- Goodbye.
		- Seek therapy. I'm too busy pumpin these numbers up to deal with your shit, you fictitious weirdo.
			COAL: Seek therapy?! Like I'm some kinda... some kinda--[wait=0.5][i]somethin-or-other?![/i]
			COAL: Oh, you're right, of course. [i]Like usual.[/i]
			COAL: Anyway, thanks for chatting. Good luck with your pumpin, chief! [speed=0.25]...[speed=1][font_size=10]I shouldn't have said that.
	- It's like {{get_lored_name("iron_ore")}} said. You're disgusting.
		COAL: Ah, yeah. I kind of figured you'd say that.
		COAL: I... gotta go. See you around, chief.
		if get_var("got_grief_coal_dating_finale") == false
			do set_var("got_grief_coal_dating_finale", true)
			do add_currency("grief", 1)
			You receive [b]1 {{get_currency_name("grief")}}[/b].
- Give it up, you're going to die alone.
	Unfortunately, your connection was terminated before your message was sent. The source of the severance is unclear.
	... A letter has been delivered to you from an unknown source. Do you accept it?
	- Yes.
		You take the red, velvet envelope in your hands. It is embossed with gold beads, and fine gold thread lines the corners and bends.
		- Take the letter out of the envelope.
		The stamp sealing the envelope closed appears as a crystal blue diamond, though you know it must only be wax. You peel up the folds of the envelope, revealing its secret message intended only for your eyes.
		You take out a folded note of goatskin parchment. Its coarse texture and strange aroma indicates the rarity and value of such a type of paper.
		- Unfold the parchment and read the letter.
		Thinner than cardboard, thicker than paper, you can hear the letter crack and thrum as you unfold it. On its sepia-colored surface, in thick, black ink, you read the following hand-written words\:
		no, u
	- No.
=> END!


~ coal_dating_help_hub_2
COAL: Who should we ask next?
- {{get_lored_name("copper")}} => coal_dating_1
- {{get_lored_name("iron_ore")}} => coal_dating_2
- {{get_lored_name("copper_ore")}} => coal_dating_3


~ coal_dating_1
COAL: [shake rate=20 level=10]{{get_lored_name("copper")}}!!![/shake]
COPPER: D\:
COAL: Why don't I have a girlfriend?
COPPER: Why don't you... oh, [i]dude![/i] I've been asking that about you for [i]years,[/i] man! Yeah!! Why [i]don't[/i] you have a girlfriend?!
COPPER: I mean, you work non-stop, shoveling {{get_currency_name("coal")}}, you get super pissed at random, and you have really cool flexy-arms, bro!! What's not to like?! You're a catch and a half!
COAL: Wow... I feel that, though this has been a waste of time, I.. I...! ...
COAL: No, nothing changed. Whatever, man. I don't have a girlfriend, and that's all right with me.
=> coal_dating_help_hub


~ coal_dating_2
COAL: [speed=0.5][font_size=10]Are you sure? That guy's a [b]fucking weirdo[/b]--[next=3.0]
IRON_ORE: I heard that, you [i]cretin!![/i]
IRON_ORE: So, you don't have a girlfriend, huh, you purple freak? Well, let's pinpoint the problem, if it's [i]at all[/i] possible.
IRON_ORE: First of all, you're a stickfigure. No chick is going to want to have you fiddling her diddlies, if you catch my drift.
IRON_ORE: That's not to imply that 2d fellas are off-limits, understand? There are plenty of cooked girlies out there who would love to date--[i]or otherwise [b]interact with[/b][/i]--people like Gintoki, Itachi, Gojo, Dabi, Bakugo, and [i]don't even get me started on [b]Levi[/b][/i].
IRON_ORE: But for them to want... [i]you...[/i] They would have to be cooked out of their minds. They would have to be [b]so far gone beyond belief[/b] that they would be--or should be--[b][speed=0.5]locked up in a maximum-security [speed=0.2][shake rate=20 level=10]psychiatric penetentiary!!![/shake][/b]
IRON_ORE: [i]...Bitch![/i]
IRON_ORE: Second of all, you--hey, where are you going?
COAL: Well, that was unpleasant.
COAL: I'm going to go crawl under something heavy and all-encompassing. A rock, maybe. Or a car. Where's the nearest high-traffic road?
- Don't take it too hard. And don't quit working, I need more {{get_currency_name("coal")}}.
	COAL: Gee, thanks for your concern for my well-being. Can't let my recent [b]mega-roasting[/b] hit your bottom line, huh? I see how it is.
	COAL: And you're right!
- Hey, I'd date you.
	COAL: What... really?!
	- Yep.
		do set_speaker_effects("[shake rate=20 level=10]%s")
		COAL: ...
		do set_speaker_effects("[shake rate=20 level=10]%s")
		COAL: [shake rate=20 level=10]HELL YES![/shake]
		COAL: Wait, are you a chick?
		- Yes.
			do set_speaker_effects("[wave amp=25 freq=5]%s")
			COAL: [wave amp=25 freq=5]Wooooooooo-hooooooooo!!![/wave]
			COAL: Wait--what's going o--
			DEV: Sorry, you horny bastard, but I can't let you date any of the LOREDs. I allotted no development time for dating mode.
			DEV: *B[do DialogueHandbook.play_sound("bonk")]onk.*
		- No.
			COAL: ... [wait=3] ...
			COAL: Uh...
			COAL: Sorry, chief! I considered it, and my internalized homophobia won't let me proceed any further! Gonna have to reject you.
	- Nope.
		COAL: Yess!!--Wait, wh-what?!
		COAL: [shake rate=20 level=10][b]Why did you say it, then?!![/b][/shake]
		COAL: Right after I got blasted by {{get_lored_name("iron_ore")}}, too! That's just cruel!
		COAL: Well, anyway...
=> coal_dating_help_hub


~ coal_dating_3
COAL: Hey, {{get_lored_name("copper_ore")}}.
{{get_lored_name("copper_ore")}} cannot hear you amidst the roar of clanging pickaxes, burrowing drills, and collapsing tunnels.
- [b]Hey, {{get_lored_name("copper_ore")}}!!![/b]
No response.
COAL: Buddy... I got this.
{{get_lored_name("coal")}} cracks his knuckles, for some reason.
do set_speaker_effects("[shake rate=20 level=10]%s")
COAL: [wave amp=25 freq=5][shake rate=20 level=10][b]YOU LAZY, WIFE-BEATING FAT-CAT!!! I HEARD YOU WERE A CLOSETED GAY!!! YOU BEAT YOUR SON, TOO?! HOW'S MINIMUM WAGE TREATING YOU, SUCKER? REMIND ME HOW MANY YEARS REMAIN ON YOUR MORTGAGE!![/b][/shake][/wave]
Throughout {{get_lored_name("coal")}}'s yells, {{get_lored_name("copper_ore")}} made no reaction to indicate he heard any of it. When the shouting had finished, however, it was as if his brain [i]had[/i] heard all of it, and he subsequently dropped over, so overcome with anger that his nervous system shut down to prevent catastrophe.
COAL: I'll be honest, I'm glad that whatever just happened to him happened to him. I was regretting what I was saying [i]as I was saying it[/i].
- I was regretting what you were saying, too! Let's get out of here before he wakes up.
	COAL: Good idea. This whole operation was a bust. [i]Book it!!![/i]
- Because it was so rude??
	COAL: What? No! I mean, of course it was, but he's the kind of person who would respond to those kinds of words! My brain is huge, chief, don't forget it.
	COAL: I'm relieved because I'm pretty sure he would have killed me, and you for standing next to me.
	COAL: I hope he doesn't remember what I said when he wakes up. Let's get out of here! Run!!
	- Run away.
=> coal_dating_help_hub


#endregion